apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId "com.pinkira.pinkyranotes"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "mock"

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            applicationIdSuffix = ".mock"
            dimension "mock"
        }
        prod {

        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true)
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_7
        sourceCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support Libs & Design
    implementation 'com.android.support:appcompat-v7:' + rootProject.supportLibraryVersion
    implementation 'com.android.support:support-v4:' + rootProject.supportLibraryVersion
    implementation 'com.android.support:design:' + rootProject.supportLibraryVersion
    implementation 'com.android.support.constraint:constraint-layout:' + rootProject.constraintLayoutVersion
    implementation 'com.android.support:cardview-v7:' + rootProject.cardViewVersion

    // Architecture Components
    implementation 'android.arch.lifecycle:extensions:' + rootProject.archLifecycleVersion
    annotationProcessor 'android.arch.lifecycle:compiler:' + rootProject.archLifecycleVersion

    // Room
    implementation 'android.arch.persistence.room:runtime:' + rootProject.archRoomVersion
    annotationProcessor 'android.arch.persistence.room:compiler:' + rootProject.archRoomVersion

    // Butterknife
    implementation 'com.jakewharton:butterknife:' + rootProject.butterknifeVersion
    annotationProcessor 'com.jakewharton:butterknife-compiler:' + rootProject.butterknifeVersion

    // TESTING
    // JUnit & Runner
    testImplementation 'junit:junit:' + rootProject.junitVersion
    androidTestImplementation 'com.android.support.test:runner:' + rootProject.runnerVersion
    // Espresso
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:' + rootProject.espressoVersion
}
